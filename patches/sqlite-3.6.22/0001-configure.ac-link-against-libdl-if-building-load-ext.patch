From 48c967d9af0c58482c8715da22f6ccdefb3f2904 Mon Sep 17 00:00:00 2001
From: Marc Kleine-Budde <mkl@pengutronix.de>
Date: Sun, 14 Feb 2010 00:21:07 +0100
Subject: [PATCH] configure.ac: link against libdl if building load extension

From:
http://patch-tracker.debian.org/package/sqlite3/3.6.22-1

Signed-off-by: Marc Kleine-Budde <mkl@pengutronix.de>
---
 configure.ac |   32 ++++++++++++++++++++++++++++++++
 1 files changed, 32 insertions(+), 0 deletions(-)

diff --git a/configure.ac b/configure.ac
index 441cf78..a8e1be4 100644
--- a/configure.ac
+++ b/configure.ac
@@ -233,6 +233,38 @@ fi
 AC_SUBST(BUILD_CC)
 
 ##########
+# Do we want to support load_extension()?
+#
+AC_ARG_ENABLE(load-extension,
+AC_HELP_STRING([--enable-load-extension],
+	       [Include SQL functions for loading extension libraries]),,
+              enable_load_extension=auto)
+
+if test "x$enable_load_extension" = xyes ||
+   test "x$enable_load_extension" = xauto; then
+
+  can_load_extension=yes
+  # libtool will already have looked for <dlfcn.h>
+  if test $ac_cv_header_dlfcn_h = no; then
+    can_load_extension=no
+  else
+    AC_SEARCH_LIBS(dlopen, dl, , [can_load_extension=no])
+  fi
+elif test "x$enable_load_extension" = xno; then
+  can_load_extension=no
+else
+  AC_MSG_ERROR([invalid argument to --enable-load-extension])
+fi
+
+if test $can_load_extension = no; then
+  if test "x$enable_load_extension" = xyes; then
+    AC_MSG_ERROR([<dlfcn.h> routines missing, load_extension() not supported])
+  fi
+  AC_DEFINE(SQLITE_OMIT_LOAD_EXTENSION, 1,
+            [Define if the load_extension() sql function should be omitted.])
+fi
+
+##########
 # Do we want to support multithreaded use of sqlite
 #
 AC_ARG_ENABLE(threadsafe, 
-- 
1.6.6.2

