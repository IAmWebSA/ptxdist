#!/usr/bin/kermit

define ptx_check_fail {
	if failure {
		writeln ERROR "FAILED! (\%1)"
		exit 1
	}
}

define ptx_init {
	# get boardsetup variables
	take \%1

	writeln ERROR "Communicating via \m(PTXCONF_BOARDSETUP_SERIALPORT) at \m(PTXCONF_BOARDSETUP_SERIALBAUDRATE) bps."
	writeln ERROR

	# ':' distinguishes between serial line and telnet port
	if not match \m(PTXCONF_BOARDSETUP_SERIALPORT) *:* {
		set line \m(PTXCONF_BOARDSETUP_SERIALPORT)
		ptx_check_fail "Could not setup serial port (\m(PTXCONF_BOARDSETUP_SERIALPORT))!"
		set speed \m(PTXCONF_BOARDSETUP_SERIALBAUDRATE)
		set parity none
		set stop-bits 1
	} else {
		set host \m(PTXCONF_BOARDSETUP_SERIALPORT)
		set terminal echo remote
	}
	set carrier-watch off
	set handshake none
	set flow-control none
	robust

	set input cancellation off
	set input case observe
	set input buffer-length 16384

	writeln ERROR "==============================="
	writeln ERROR "Please power on your board now!"
	writeln ERROR "==============================="
	writeln ERROR
}

define ptx_exit {

	writeln ERROR "All OK!"
	exit 0
}

define ptx_test_start {

	# do padding with '.'
	write ERROR "\frpad(\%1,40,.)"
}

define ptx_test_end {

	writeln ERROR "OK"
}

define ptx_wait_string {

	input \%1 \%2
	ptx_check_fail "waiting for '\%2'"
}

define ptx_uboot_enter_shell {

	ptx_test_start "Logging into U-Boot"

	minput 120 "U-Boot 1" "U-Boot 2"
:eval_again
	ptx_check_fail "waiting for u-boot-signature"
	lineout
	.uboot_version := \v(minput)
	if = \m(uboot_version) 2 {
		.uboot_prompt = "uboot:"
	} else {
		.uboot_prompt = "uboot> "
	}
	# sometimes there are caches for serial inputs containing old data
	# make sure we act on the newest one
	minput 120 "U-Boot 1" "U-Boot 2" \m(uboot_prompt)
	if not = \v(minput) 3 goto eval_again
	ptx_test_end
}

define ptx_uboot_exec {

	lineout "\%2"
	input \%1 \m(uboot_prompt)
	ptx_check_fail "timeout while waiting for u-boot prompt after '\%2'"

	if = \m(uboot_version) 2 {
		lineout "echo result: $?"
		input 10 \fpattern(result: 0$)

		ptx_check_fail "returncode from '\%2' is not 0"

		input 10 \m(uboot_prompt)
	}
}

define ptx_uboot_exec_single {

	ptx_test_start "\%1"
	ptx_uboot_exec "\%2" "\%3"
	ptx_test_end
}

define ptx_wait_string_single {

	ptx_test_start "\%1"
	ptx_wait_string "\%2" "\%3"
	ptx_test_end
}
