diff -ur Libnet-latest-orig/acinclude.m4 Libnet-latest/acinclude.m4
--- Libnet-latest-orig/acinclude.m4	Tue Aug  6 00:26:03 2002
+++ Libnet-latest/acinclude.m4	Sun Sep  7 19:08:02 2003
@@ -19,7 +19,7 @@
 
 AC_DEFUN(AC_LIBNET_CHECK_PF_PACKET,
 [
-    AC_MSG_CHECKING(for packet socet (PF_SOCKET))
+    AC_MSG_CHECKING(for packet socket (PF_SOCKET))
     AC_CACHE_VAL(ac_libnet_have_packet_socket,
 
         [case "$target_os" in
@@ -78,14 +78,14 @@
     else
         ac_libnet_have_packet_socket=`./pf_packet-test`;
     fi
-    if test $ac_libnet_have_packet_socket = choked; then
+    if test x$ac_libnet_have_packet_socket = x; then
+        AC_MSG_RESULT([test program run choked... if cross-compiling, set ac_libnet_have_packet_socket to yes or no])
+    elif test $ac_libnet_have_packet_socket = choked; then
         AC_MSG_RESULT(test program compile choked... assuming no)
     elif test $ac_libnet_have_packet_socket = yes; then
         AC_MSG_RESULT(yes)
-        AC_DEFINE(HAVE_PACKET_SOCKET)
     elif test $ac_libnet_have_packet_socket = probably; then
         AC_MSG_RESULT(test program got EPERM... assuming yes)
-        AC_DEFINE(HAVE_PACKET_SOCKET)
     elif test $ac_libnet_have_packet_socket = no; then
         AC_MSG_RESULT(no)
     fi
@@ -93,6 +93,11 @@
     rm -f pf_packet-test* core core.pf_packet-test
     ;;
     esac])
+    if test x$ac_libnet_have_packet_socket = xyes; then
+        AC_DEFINE(HAVE_PACKET_SOCKET)
+    elif test x$ac_libnet_have_packet_socket = xprobably; then
+        AC_DEFINE(HAVE_PACKET_SOCKET)
+    fi
 ])
 
 dnl
@@ -242,87 +247,6 @@
     fi
 ])
 
-
-dnl
-dnl Checks endianess
-dnl
-dnl usage:
-dnl
-dnl     AC_LIBNET_ENDIAN_CHECK
-dnl
-dnl results:
-dnl
-dnl     LIBNET_BIG_ENDIAN = 1   or
-dnl     LIBNET_LIL_ENDIAN = 1
-dnl
-
-AC_DEFUN(AC_LIBNET_ENDIAN_CHECK,
-    [AC_MSG_CHECKING(machine endianess)
-
-    cat > conftest.c << EOF
-#       include <stdio.h>
-#       include <stdlib.h>
-
-        int main()
-        {
-            union
-            {
-                short s;
-                char c[[sizeof(short)]];
-            } un;
-
-            un.s = 0x0102;
-            if (sizeof (short) == 2)
-            {
-                if (un.c [[0]] == 1 && un.c [[1]] == 2)
-                {
-                    printf("B\n");
-                }
-                else
-                {
-                    if (un.c [[0]] == 2 && un.c [[1]] == 1)
-                    {
-                        printf("L\n");
-                    }
-                }
-            }
-            else
-            {
-                printf("?\n");
-            }
-            return (EXIT_SUCCESS);
-        }
-EOF
-        ${CC-cc} -o conftest $CFLAGS $CPPFLAGS $LDFLAGS conftest.c $LIBS > /dev/null 2>&1
-        # Oopz 4.3 BSD doesn't have this.  Sorry.
-        if test ! -x conftest ; then
-dnl failed to compile for some reason
-            ac_cv_libnet_endianess=unknown
-        else
-            ./conftest > conftest.out
-            result=`cat conftest.out`
-            if test $result = "B"; then
-                ac_cv_libnet_endianess=big
-            elif test $result = "L"; then
-                ac_cv_libnet_endianess=lil
-            else
-                ac_cv_libnet_endianess=unknown
-            fi                                
-        fi
-        rm -f conftest* core core.conftest
-
-        AC_MSG_RESULT($ac_cv_libnet_endianess)
-
-        if test $ac_cv_libnet_endianess = big ; then
-            AC_DEFINE(LIBNET_BIG_ENDIAN)
-            ENDIANESS="LIBNET_BIG_ENDIAN"
-            LIBNET_CONFIG_DEFINES="$LIBNET_CONFIG_DEFINES -DLIBNET_BIG_ENDIAN"
-        elif test $ac_cv_libnet_endianess = lil ; then
-            AC_DEFINE(LIBNET_LIL_ENDIAN)
-            ENDIANESS="LIBNET_LIL_ENDIAN"
-            LIBNET_CONFIG_DEFINES="$LIBNET_CONFIG_DEFINES -DLIBNET_LIL_ENDIAN"
-        fi
-    ])
 
 dnl
 dnl Improved version of AC_CHECK_LIB
diff -ur Libnet-latest-orig/configure.in Libnet-latest/configure.in
--- Libnet-latest-orig/configure.in	Tue Aug  6 00:26:03 2002
+++ Libnet-latest/configure.in	Sun Sep  7 19:14:31 2003
@@ -7,6 +7,7 @@
 dnl Process this file with autoconf to produce a configure script.
 
 
+AC_PREREQ(2.52)
 AC_INIT(src/libnet_build_ip.c)
 LIBNET_VERSION=`cat VERSION`
 AC_MSG_RESULT(beginning autoconfiguration process for libnet-$LIBNET_VERSION...)
@@ -24,17 +25,21 @@
 
 AC_CHECK_HEADERS(sys/sockio.h)
 
-dnl
-dnl And some custom things
-dnl
+AC_C_BIGENDIAN([
+	AC_DEFINE(LIBNET_BIG_ENDIAN)
+	ENDIANESS="LIBNET_BIG_ENDIAN"
+	LIBNET_CONFIG_DEFINES="$LIBNET_CONFIG_DEFINES -DLIBNET_BIG_ENDIAN"
+],[
+	AC_DEFINE(LIBNET_LIL_ENDIAN)
+	ENDIANESS="LIBNET_LIL_ENDIAN"
+	LIBNET_CONFIG_DEFINES="$LIBNET_CONFIG_DEFINES -DLIBNET_LIL_ENDIAN"
+], AC_MSG_WARN(cannot detect endianess. try setting ac_cv_c_bigendian to yes or no))
 
-AC_LIBNET_ENDIAN_CHECK
 AC_LBL_UNALIGNED_ACCESS
 dnl AC_LBL_LIBRARY_NET
 
 AC_SUBST(ENDIANESS)
 AC_SUBST(LIBNET_VERSION)
-AC_SUBST(LIBOBJS)
 AC_SUBST(HAVE_PACKET_SOCKET)
 AC_SUBST(ADDITIONAL_LIBS)
 AC_SUBST(LIBNET_CONFIG_DEFINES)
@@ -68,39 +73,39 @@
 AC_MSG_CHECKING(link-layer packet interface type)
 
 if test -r /dev/bpf0 ; then
-    LIBOBJS="$LIBOBJS libnet_link_bpf.o"
+    AC_LIBOBJ(libnet_link_bpf)
     AC_MSG_RESULT(found bpf)
 elif test -r /usr/include/net/pfilt.h ; then
-    LIBOBJS="$LIBOBJS libnet_link_pf.o"
+    AC_LIBOBJ(libnet_link_pf)
     AC_MSG_RESULT(found pf)
 elif test -r /dev/nit ; then
-    LIBOBJS="$LIBOBJS libnet_link_snit.o"
+    AC_LIBOBJ(libnet_link_snit)
     AC_MSG_RESULT(found snit)
 elif test -r /usr/include/sys/net/nit.h ; then
-    LIBOBJS="$LIBOBJS libnet_link_nit.o"
+    AC_LIBOBJ(libnet_link_nit)
     AC_MSG_RESULT(found nit)
 elif test -r /usr/include/net/raw.h ; then
-    LIBOBJS="$LIBOBJS libnet_link_snoop.o"
+    AC_LIBOBJ(libnet_link_snoop)
     AC_MSG_RESULT(found snoop)
 elif test -r /usr/include/sys/dlpi.h ; then
-    LIBOBJS="$LIBOBJS libnet_link_dlpi.o"
+    AC_LIBOBJ(libnet_link_dlpi)
     AC_MSG_RESULT(found dlpi)
     AC_DEFINE(HAVE_DLPI)
 elif test -r /usr/include/linux/socket.h ; then
-    LIBOBJS="$LIBOBJS libnet_link_linux.o"
+    AC_LIBOBJ(libnet_link_linux)
     AC_MSG_RESULT(found linux primitives)
     AC_LIBNET_CHECK_PF_PACKET
 elif test -c /dev/bpf0 ; then           # check again in case not readable
-    LIBOBJS="$LIBOBJS libnet_link_bpf.o"
+    AC_LIBOBJ(libnet_link_bpf)
     AC_MSG_RESULT(found bpf)
 elif test -c /dev/nit ; then            # check again in case not readable
-    LIBOBJS="$LIBOBJS libnet_link_snit.o"
+    AC_LIBOBJ(libnet_link_snit)
     AC_MSG_RESULT(found snit)
 elif test "$target_os" = "cygwin" ; then
-     LIBOBJS="$LIBOBJS libnet_link_win32.o"
+     AC_LIBOBJ(libnet_link_win32)
      AC_MSG_RESULT(found win32 wpcap)
 else
-    LIBOBJS="$LIBOBJS libnet_link_none.o"
+    AC_LIBOBJ(libnet_link_none)
     AC_MSG_WARN(could not find a link-layer packet interface)
     AC_MSG_WARN(link-layer packet injection will not be available)
 fi
@@ -218,9 +223,10 @@
 
 esac
 
-AC_OUTPUT(Makefile man/Makefile src/Makefile include/Makefile \
-    include/libnet/Makefile sample/Makefile version.h include/libnet.h \
-    libnet-config, chmod +x libnet-config)
+AC_CONFIG_FILES(Makefile man/Makefile src/Makefile include/Makefile \
+    include/libnet/Makefile sample/Makefile version.h include/libnet.h)
+AC_CONFIG_FILES(libnet-config, [chmod +x libnet-config])
+AC_OUTPUT
 
 AC_MSG_RESULT(Please note this is libnet 1.1.x.  Any code written for 1.0.x\
  will NOT work without porting.  Read the MIGRATION document for more info.)
